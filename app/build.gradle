apply plugin: 'com.android.application'

apply plugin: 'kotlin-android'
apply plugin: 'kotlin-kapt'
apply plugin: 'kotlin-android-extensions'
apply plugin: 'kotlinx-serialization'
apply plugin: "androidx.navigation.safeargs"
apply plugin: 'com.google.gms.google-services'
apply plugin: 'newrelic'

android {
    compileSdkVersion 28
    defaultConfig {
        applicationId "com.venuenext.demo"
        minSdkVersion 21
        targetSdkVersion 28
        versionCode 1
        versionName "0.1.6"
        testInstrumentationRunner "android.support.test.runner.AndroidJUnitRunner"
    }
    signingConfigs {
        release {
            def propertiesFile = project.rootProject.file('keystore.properties')
            def keystoreFile = project.rootProject.file('intel.jks')
            if (propertiesFile.exists() && keystoreFile.exists()) {
                Properties properties = new Properties()
                properties.load(propertiesFile.newDataInputStream())

                storeFile file(properties.getProperty('intel.key.store'))
                storePassword properties.getProperty('intel.key.store.password')
                keyAlias properties.getProperty('intel.key.alias')
                keyPassword properties.getProperty('intel.key.alias.password')
            }
        }
    }
    buildTypes {
        release {
            signingConfig signingConfigs.release
            minifyEnabled false
            proguardFiles getDefaultProguardFile('proguard-android-optimize.txt'), 'proguard-rules.pro'
        }
    }
    dataBinding {
        enabled = true
    }
}

buildscript {
    ext {
        new_relic_version = '5.23.0'
    }
    repositories {
        mavenCentral()
    }
    dependencies {
        classpath "com.newrelic.agent.android:agent-gradle-plugin:${new_relic_version}"
    }
}

repositories {
    jcenter()

    maven {
        url "https://venuenext.jfrog.io/venuenext/venuenextsdk-android"
        credentials {
            username = venuenextsdk_user
            password = venuenextsdk_password
        }
    }
}

dependencies {
    implementation fileTree(dir: 'libs', include: ['*.jar'])

    //venuenext
    implementation 'com.venuenext:vncore:0.1.6'
    implementation 'com.venuenext:vncoreui:0.1.6'
    implementation 'com.venuenext:vnanalytics:0.1.6'
    implementation 'com.venuenext:vnlegacy:0.1.6'
    implementation 'com.venuenext:vnorder:0.1.6'
    implementation 'com.venuenext:vnorderui:0.1.6'
    implementation 'com.venuenext:vnpayment:0.1.6'
    //kotlin
    implementation "org.jetbrains.kotlin:kotlin-stdlib-jdk8:$kotlin_version"
    implementation "org.jetbrains.kotlin:kotlin-reflect:$kotlin_version"
    implementation "org.jetbrains.kotlinx:kotlinx-serialization-runtime:$serialization_version"
    implementation "org.jetbrains.kotlinx:kotlinx-coroutines-core:$kotlin_coroutines_version"
    implementation "org.jetbrains.kotlinx:kotlinx-coroutines-android:$kotlin_coroutines_version"
    //ui
    implementation 'com.google.android.material:material:1.0.0'
    implementation 'androidx.appcompat:appcompat:1.0.2'
    implementation 'androidx.core:core-ktx:1.0.2'
    implementation 'androidx.constraintlayout:constraintlayout:1.1.3'
    implementation 'androidx.legacy:legacy-support-v4:1.0.0'
    implementation "com.github.bumptech.glide:glide:4.9.0"
    //lifecycle
    implementation "androidx.lifecycle:lifecycle-extensions:2.0.0"
    //navigation
    implementation "android.arch.navigation:navigation-ui-ktx:1.0.0"
    implementation "android.arch.navigation:navigation-fragment:1.0.0"
    implementation "android.arch.navigation:navigation-fragment-ktx:1.0.0"
    //room
    implementation "androidx.room:room-runtime:2.1.0"
    //firebase
    implementation 'com.google.firebase:firebase-core:17.0.1'
    implementation 'com.google.firebase:firebase-messaging:19.0.1'
    implementation 'com.google.android.gms:play-services-tagmanager:17.0.0'
    //braintree
    implementation 'com.braintreepayments.api:braintree:3.0.0'
    implementation 'com.braintreepayments.api:drop-in:4.1.0'
    //facebook
    implementation 'com.facebook.shimmer:shimmer:0.4.0'
    //new relic
    api "com.newrelic.agent.android:android-agent:$new_relic_version"
    //qr codes
    implementation 'com.journeyapps:zxing-android-embedded:3.6.0'

    testImplementation 'junit:junit:4.12'
    androidTestImplementation 'androidx.test:runner:1.2.0'
    androidTestImplementation 'androidx.test.espresso:espresso-core:3.2.0'
}
